plugins {
    id 'java'
}

group = 'pl.edu.icm.heap.kite'
version = '1.0'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility '17'
    targetCompatibility '17'
}

dependencies {
    implementation 'pl.edu.icm.pcj:pcj:5.3.3'
    annotationProcessor 'pl.edu.icm.pcj:pcj:5.3.3'
}

tasks.register('copyDependencies', Copy) {
    group 'build'
    description 'Copy dependencies into build directory'

    from configurations.runtimeClasspath
    into layout.buildDirectory.dir("libs")
}
jar.finalizedBy('copyDependencies')

jar {
    manifest {
        attributes 'Main-Class': 'pl.edu.icm.heap.kite.PcjMain'
    }
}

tasks.register('createFatJar', Jar) {
    group = 'build'
    description 'Create one big (fat) jar with all dependencies in it'

    manifest {
        attributes 'Main-Class': 'pl.edu.icm.heap.kite.PcjMain'
    }

    archiveBaseName = 'hpv-kite'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
